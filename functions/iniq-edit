#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

# Bootstrap complete exec environment.
builtin emulate -L zsh -o extendedglob
0=$Plugins[IQ_DIR]/iniq-edit

local var_name=$1 iq_file=$2 iq_key=$3 iq_value iq_set_value=$4
local -A Opts=( "${(kv@)${(Pkv@)var_name}}" ) INI
integer exval
if [[ ! -f $iq_file ]]; then
    msg {208}File %B{39}$iq_file{208}%b doesn\'t exist, exiting…
    return 3
fi

@str-read-toml "$iq_file" INI
exval=$?
local -a order=( "$reply[@]" )

(( exval )) && msg {208}Warning:{204}Found {41}$exval{204} errors \
    while processing {39}$iq_file:t
if [[ -z $INI || -z $reply ]]; then
    msg {208}Error: could\'t process file {41}$iq_file{208}, no data found.
    return 5
fi

if [[ -z $iq_file || -z $iq_key || -z $iq_set_value ]]; then
    msg {208}Error:%f expected 2 arguments – the key to edit and the value to\
        set, exiting…
    return 1
fi

if (($+INI[$iq_key])); then
    INI[$iq_key]=$iq_set_value
else
    msg {208}Error:{203} no such key {41}%B${iq_key##*__}{203}%b, cannot edit
    return 7
fi

@str-write-toml "$iq_file" INI order
exval=$?
((exval&&exval!=127))&&msg {208}Warning:{204}Found {41}$exval{204} errors \
    while writing %B{39}$iq_file{204}…
((!exval))&&msg {41}File %B{203}$iq_file:t{41}%b edited correctly
return exval