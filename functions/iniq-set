#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2022 Sebastian Gniazdowski

# Bootstrap complete exec environment.
builtin emulate -L zsh -o extendedglob
0=$Plugins[IQ_DIR]/iniq-edit

local var_name=$1 iq_file=$2 iq_key=$3 iq_value iq_set_value=$4 sect=${3%%/*}
local -A Opts=( "${(kv@)${(Pkv@)var_name}}" ) INI
integer exval iq_existed
if [[ ! -f $iq_file ]]; then
    iqmsg {208}INI file %B{39}$iq_file{208}%b doesn\'t exist, exiting…
    return 3
fi

@str-read-toml "$iq_file" INI
exval=$?
local -a order=( "$reply[@]" )

(( exval )) && iqmsg {208}Warning:{204}Found {41}$exval{204} errors \
    while processing {39}$iq_file:t

if [[ -z $INI || -z $reply ]]; then
    iqmsg {208}Error: could\'t process INI file {41}$iq_file{208}, no data found.
    return 5
fi

if [[ -z $iq_file || -z $iq_key || -z $iq_set_value ]]; then
    iqmsg {208}Error:%f expected 3 arguments – the file to edit, the key to change \
        and the value to set, exiting…
    return 1
fi
iq_key=${iq_key//\//__}
(($+INI[$iq_key]))&&{iq_value=$INI[$iq_key];iq_existed=1;}
INI[$iq_key]=$iq_set_value
if ((!iq_existed)); then
    if (($+Options[-b]+$+Options[--bottom] )); then
        integer i=${order[(I)${sect}__*]}+1
    else
        integer i=${order[(i)${sect}__*]}
    fi
    order=("${(@)order[1,i-1]}" $iq_key "${(@)order[i,-1]}")
fi
#printf %s\\n $order
@str-write-toml "$iq_file" INI order
exval=$?
((exval&&exval!=127))&&iqmsg {208}Warning:{204}Found {41}$exval{204} errors \
    while writing %B{39}$iq_file{204}…
if ((!iq_existed)); then
    if ((!exval)); then
        iqmsg {41}File %B{203}$iq_file:t{41}%b edited correctly, a new key has \
            been added: in section %B{39}\[$sect\]{41} → %B{208}${iq_key##*__}{33}={208}$iq_set_value%b{41}.
    fi
else
    if ((!exval)); then
        iqmsg {41}File %B{203}$iq_file:t{41}%b edited correctly, existing key \
            %B{203}${iq_key##*__}%b{41} in section %B{39}\[$sect\]{41} \
            set to → %B{208}${iq_key##*__}{33}={208}$iq_set_value%b{41}.

    fi
fi
return exval